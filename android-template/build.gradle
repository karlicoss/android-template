buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'me.tatarka:gradle-retrolambda:3.2.5'

        // see https://github.com/evant/android-retrolambda-lombok https://github.com/evant/android-retrolambda-lombok
        classpath 'me.tatarka.retrolambda.projectlombok:lombok.ast:0.2.3.a2'
    }
    // Exclude the version that the android plugin depends on.
    configurations.classpath.exclude group: 'com.android.tools.external.lombok'
}

apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.github.karlicoss.template"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }

    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    compile libraries.rxJava

    testCompile libraries.junit
    testCompile libraries.assertJ
    testCompile libraries.robolectric

    retrolambdaConfig libraries.retrolambda
}

retrolambda {
    defaultMethods true
}


apply plugin: 'findbugs'

task findbugs(type: FindBugs, dependsOn: assembleDebug) {
    excludeFilter project.rootProject.file('config/findbugs/suppressions.xml')

    ignoreFailures = false
    effort = 'max'
    reportLevel = 'low'

    classes = project.fileTree('build/intermediates/classes/')
    classpath = project.files(project.configurations.testCompile)
    source = project.files('src')

    reports {
        xml {
            enabled = false
            withMessages = true
        }
        // For some reason, only one type report is allowed
        // If you want to get a human readable report HTML report, just disable XML
        html {
            enabled = !xml.enabled
        }
    }
}


import com.github.karlicoss.gradlestuff.GradleDemoPlugin

apply plugin: GradleDemoPlugin